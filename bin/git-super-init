#!/bin/zsh

gBinDir=$(dirname $(realpath $0))
gAssetsDir=$(realpath ${gBinDir}/../assets)

function init_git_folder {
	git init
	git switch-main --create
}

function create_license {
	local year=$(date +%Y)
	local author=$(git config user.name)
	local url=$(git config user.web)

	(
		echo "# The MIT License"
		echo
		echo "Copyright $year $author $url"
		echo
		cat $gAssetsDir/mit-license
	) >LICENSE.md
}

function create_readme {
	local name="$1"
	local description="$2"

	(
		echo "# $name"
		[ -n "$description" ] && echo "$description"
		echo
		echo "## License"
		echo "Available under the [MIT license](LICENSE.md)."
	) >README.md
}

function create_gitignore {
	local type="$1"
	local baseUrl="https://www.toptal.com/developers/gitignore/api/"

	local osIgnores=(linux macos windows)

	local editorIgnores=(jetbrains+all sublimetext vim visualstudiocode)

	local javaIgnores=(java maven)
	local nodeIgnores=(node)
	local phpIgnores=(composer)
	local pythongIgnores=(python)
	local rustIgnores=(rust)

	local typedIgnoredVariable=${type}Ignores
	local typedIgnores=${(P)typedIgnoredVariable}

	local ignores=($osIgnores $typedIgnores $editorIgnores)

	curl --silent --show-error --location $baseUrl${(j:,:)ignores} >.gitignore
}

function extend_gitignore {
	cat $gAssetsDir/gitignore-extension >>.gitignore
}

function create_ignored-folder {
	local folder="$1"

	mkdir --parent "$folder"
	touch "$folder/.gitkeep"
}

function create_commit {
	local name="$1"

	git add .
	git commit --message="build($name): initial commit"
}

name=$(basename $PWD)
type="$1"
description="$2"

init_git_folder
create_license
create_readme "$name" "$description"

create_gitignore "$type"
extend_gitignore

create_ignored-folder .tmp

create_commit "$name"
