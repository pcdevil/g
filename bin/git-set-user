#!/bin/zsh

function get_origin-url {
	git config --get remote.origin.url 2>/dev/null
}

function get_short-url {
	local domain="$1"

	if [[ -z $domain ]]; then
		return
	fi

	# remove anything before @ (deals with git@ and other username/passwords)
	domain=${domain#*@}

	# save and remove protocol
	local protocol=${domain%%://*}
	if [[ ${#protocol} == ${#domain} ]]; then
		protocol=https
	fi
	protocol=${protocol}://
	domain=${domain#*://}

	# remove port (and username if git@ url)
	domain=${domain%%:*}

	# remove pathname
	domain=${domain%%/*}

	# save and remove tld
	local tld=${domain##*.}
	if [[ ${#tld} == ${#domain} ]]; then
		tld=""
	else
		tld=.${tld}
	fi
	domain=${domain%.*}

	# remove subdomains
	domain=${domain##*.}

	echo $protocol$domain$tld
}

function get_matching_user-var {
	local variable="$1"
	local url="$2"

	git config --get-urlmatch user.$variable $url 2>/dev/null ||
		git config user.$variable
}

function set_signingkey {
	local signingkey="$1"
	local gpgBin=$(which gpg)

	git config user.signingkey "$signingkey"
	git config gpg.program "$gpgBin"
	git config commit.gpgsign true
	git config tag.gpgsign true
}

originUrl=$(get_origin-url)
repoUrl=${1:-$originUrl}
shortRepoUrl=$(get_short-url $repoUrl)

name=$(get_matching_user-var name $shortRepoUrl)
email=$(get_matching_user-var email $shortRepoUrl)
signingkey=$(get_matching_user-var signingkey $shortRepoUrl)

git config user.name "$name"
git config user.email "$email"

if [ -n "$signingkey" ]; then
	set_signingkey "$signingkey"
fi
